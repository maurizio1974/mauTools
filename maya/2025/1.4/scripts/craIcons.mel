/*
'Maya Icons' MEL script

Author: Carlos Rico Adega - carlos.rico.3d@gmail.com
First Release Date: October 22, 2014

Copyright (c) 2016 Carlos Rico Adega

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.


SCRIPT NAME:
	craIcons.mel

	Main procedure:
	    craIcons;

DESCRIPTION:
		List Maya environment variables and their values
		
LAUNCH SCRIPT:
       craIcons; //loads default icons
       source craIcons;craAllIcons; //loads all icons in Maya

CHANGE LOG:

    Version: 1.0.0
    Date: October 22, 2014
        - First release
        
    Version: 1.1.0
    Date: February 5, 2016
        - Slight UI size change to fit Maya 2016 better
*/

global proc craIconsCmd( int $mode )
{
	string $mayaVersion = `about -version`;
 	string $craIconsArray[] = `resourceManager -nameFilter "*"`;
    string $iconPaths[];
	string $iconpmPaths[];
 	
 	string $iconPathsStr = `getenv "MAYA_FILE_ICON_PATH"`;
 	tokenize $iconPathsStr ";" $iconPaths;
 	
 	string $iconpmPathsStr = `getenv "XBMLANGPATH"`;
 	tokenize $iconpmPathsStr ";" $iconpmPaths;
 	
 	if(`about -win`)
    {
	    	tokenize $iconPathsStr ";" $iconPaths;
	    	tokenize $iconpmPathsStr ";" $iconpmPaths;
	}
    	else
    	{    
        tokenize $iconPathsStr ":" $iconPaths;
        tokenize $iconpmPathsStr ":" $iconpmPaths;
    }
        
	string $craIconsPaths[] = stringArrayCatenate($iconPaths, $iconpmPaths);
	$craIconsPaths = stringArrayRemoveDuplicates($craIconsPaths);

	if($mode)
	{
        for($i in $craIconsPaths)
        {
            $craIconsArray = stringArrayCatenate($craIconsArray, getFileList("-folder",($i + "/"),"-filespec", "*.bmp"));
            $craIconsArray = stringArrayCatenate($craIconsArray, getFileList("-folder",($i + "/"),"-filespec", "*.png"));
            $craIconsArray = stringArrayCatenate($craIconsArray, getFileList("-folder",($i + "/"),"-filespec", "*.xpm"));
        }
        $craIconsArray = stringArrayRemoveDuplicates($craIconsArray);
        $craIconsArray = sort($craIconsArray);
	}
	print ("\n" + size($craIconsArray) + " icons loaded...\n");
	
	if(`window -exists craIconsWin`)
        deleteUI craIconsWin;
	
	int $craIconsNum = size($craIconsArray);
	
	if($mode)
        window -t ("All Icons in Maya " + $mayaVersion + " - " + $craIconsNum + " icons loaded") -mnb 1 -mxb 1 -tlb 1 -w 720 - h 650 -s 0 craIconsWin;//;
    else
        window -t ("Default Icons in Maya " + $mayaVersion + " - " + $craIconsNum + " icons loaded") -mnb 1 -mxb 1 -tlb 1 -w 720 - h 650 -s 0 craIconsWin;//;
    
    int $craWidth = 507;
    int $craHeight = 488;
    if(`getApplicationVersionAsFloat` > 2015)
    {
        $craWidth = 490;
        $craHeight = 484;
    }
    
    frameLayout -w $craWidth -mw 2 -mh 2 -lv 0 a1;
    columnLayout -w $craWidth -adj 1 a2;
    rowLayout -nc 4 -adj 1;
        button -w 150 -h 20 -l "Load Default Maya icons" -c "craIcons";
        button -w 150 -h 20 -ann "Loads icons from XBMLANGPATH and MAYA_FILE_ICON_PATH environment variables folders" -l "Load All Maya icons" -c "craAllIcons"; 
        button -w 150 -h 20 -l "Show icon folders " -c "craShowIconFolders";
        nodeIconButton -h 20 -w 20 -i "pickOtherObj.png" -ann "About this tool" -c "craAboutIconsTool";
    setParent ..;
    separator -h 2 -style "none";
    scrollLayout -w ($craWidth-5) -h $craHeight craIconsSL;
    
    gridLayout -nc 12 -cellWidthHeight 40 40 -bgc .26 .26 .26;
	progressWindow -ep;
	
	progressWindow -title  ("Loading Icons..." )
        -isInterruptable  1
        -max $craIconsNum
        -status  ("Preparing ... ")
    ;
	
    int $counter = 0;
	
	for ($icon in $craIconsArray)
	{ 
		progressWindow -e -status  ( $counter + "/" + $craIconsNum + " " + $icon) -pr $counter;
		if ( `progressWindow -query -isCancelled` )
		{
			progressWindow -ep;
			error "Cancelled.\n";
			return;
		}
		string $cmd = "print(\"" + $icon + "\" + \"\\n\");";
		catchQuiet(`nodeIconButton -image $icon -ann $icon -c $cmd`);
		$counter++;
	}
	
	progressWindow -ep;
	setParent ..;
	setParent ..;
        showWindow;

    if(`getApplicationVersionAsFloat` > 2015)
        window -e -wh 505 515 -s 0 craIconsWin;
    else
        window -e -wh 513 520 -s 0 craIconsWin;

}

global proc craShowIconFolders()        
{
        string $iconPaths[];
        string $iconpmPaths[];
        
        string $iconPathsStr = `getenv "MAYA_FILE_ICON_PATH"`;
        string $iconpmPathsStr = `getenv "XBMLANGPATH"`;
        
    if(`about -win`)
    {
        tokenize $iconPathsStr ";" $iconPaths;
        tokenize $iconpmPathsStr ";" $iconpmPaths;
	}
    	else
    	{    
        tokenize $iconPathsStr ":" $iconPaths;
        tokenize $iconpmPathsStr ":" $iconpmPaths;
    }
            
    string $craIconsPaths[] = sort(stringArrayCatenate($iconPaths, $iconpmPaths));
    $craIconsPaths = stringArrayRemoveDuplicates($craIconsPaths);
    
    print $craIconsPaths;
    if (`window -exists craIconPathsWin`)
        deleteUI craIconPathsWin;
    
    window -t "Icon Paths" -s 0 -tlb 1 -wh 500 505 craIconPathsWin;
    columnLayout -w 500 -h 500 -adj 1;
        textScrollList -dcc "craIconsOpenPath" -sc "print `textScrollList -q -si craIconsPathsTSL`" -w 500 -h 500 craIconsPathsTSL;
        popupMenu;
            menuItem -l "Open path" -c "craIconsOpenPath";
        setParent -m ..;
    setParent ..;

    for($crai in $craIconsPaths)
    {
        $crai = `substituteAllString $crai "/%B" ""`;
        if( `filetest -d $crai` )
            textScrollList -e -a $crai craIconsPathsTSL;
    }

    showWindow craIconPathsWin;
    
    window -e -t ("Icon Paths (" + size(`textScrollList -q -ai craIconsPathsTSL`) + " folders)") -s 0 -tlb 1 -wh 502 502 craIconPathsWin;
    print (size(`textScrollList -q -ai craIconsPathsTSL`) + " folders. See Script Editor");
}

global proc craIconsOpenPath()
{
	string $craGetCurrentPaths[] = `textScrollList -q -si craIconsPathsTSL`;
	string $craGetCurrentPath = $craGetCurrentPaths[0];
	
    if(`about -linux`)
        system ("xdg-open " + `toNativePath $craGetCurrentPath` + "&");
    else if(`about -macOS`)
        system ("open " + `toNativePath $craGetCurrentPath`);
    else if(`about -win`)
        system ("start explorer \"" + `toNativePath $craGetCurrentPath` + "\"");
}

global proc craIcons()
{
    craIconsCmd 0;
}

global proc craAllIcons()
{
    craIconsCmd 1;
}

global proc craAboutIconsTool()
{
	if (`window -exists craAboutIconsToolWin`)
		deleteUI craAboutIconsToolWin;
	
	window -t  "About this Tool" -s 0 -tlb 1 craAboutIconsToolWin;
	frameLayout -mw 5 -mh 5 -lv 0;
	frameLayout -mw 5 -mh 5 -lv 0;
        columnLayout -adj 1;
            text -l "<font color=gold><h3>Maya Icons</h3></font>";
            text -l "<font color=steelblue><h3>v1.1.0</h3></font>";
            text -l "\nShow Maya icons";
            text -l "";
            textField -ed 0 -text "   carlos.rico.3d@gmail.com";
            text -l "";
            text -l "<font color=indianred><h4>Carlos Rico Adega &copy; 2016</h4></font>";
            text -l "";
            rowLayout -nc 2 -adj 1;
                button -w 70 -l "Downloads" -ann "Free downloads from CreativeCrash.com" -c "showHelp -absolute \"http://www.creativecrash.com/users/charliewales/contributions?tab=free_downloads\"";
                button -w 70 -ann "Linked[In] profile"-l "Linked[In]" -c "showHelp -absolute \"http://www.linkedin.com/pub/carlos-rico/6/58/325\"";
            setParent ..;
            rowLayout -nc 2 -adj 1;
                button -l "Vimeo" -ann "Vimeo Channel" -c "showHelp -absolute \"https://vimeo.com/channels/749131\"";
                button -w 70 -l "YouTube" -ann "Youtube Channel" -c "showHelp -absolute \"https://www.youtube.com/channel/UCXnSX8PHpQtwJR-uN-atO3Q\"";
            setParent ..;      
        setParent ..;
        window -e -wh 176 205 craAboutIconsToolWin;
	showWindow craAboutIconsToolWin;
}