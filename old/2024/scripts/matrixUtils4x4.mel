// --MAYA-------------------------------------------------------------------------------------------------------------------------
//
//       Copyright (c)  Lucas Digital Ltd.
//       All rights reserved. Used under authorization.
//       This matrerial contains the confidential and
//       proprietary information of Lucas Digital Lt.
//       and may not be copied in whole or in part 
//       without the express written permission of Lucas Digital Ltd.
//       This copyright notice does not imply publication.
//
/*********************************************************************************************************************************

Class:

         Matrix Utility


Name:

         matrixUtils4x4.mel


Author:

         James Tooley


Date:

         08/08/03


Description:

         Matrix helper utilities.
         Modifications:  
         		Added float16ToMatrix4x4.
         			-James R. Tooley  Aug 26, 2003
         
         		Added a bezier inverse basis matrix routine to
         		facilitate the spine rigs
         		Added the inverse matrix routine.
         		Only seems to work for basis matrix stuff.
         			-James R. Tooley  Sep 1, 2003
         
         		Added rotationMatrix and multMatrix capabilities.
         			-James R. Tooley  Jan 6, 2004




*********************************************************************************************************************************/
/*********************************************************************************************************************************
Function:

         fltArryMatrix4x4Mult


Parameters:

         float $pnt[] - Point to pass through the matrix
         matrix $m[][] - Matrix/Coordinate space to pass the point through


Returns:

         float $new[]


See Also 

         <vctrMatrix4x4Mult>
*********************************************************************************************************************************/
global proc float[] fltArryMatrix4x4Mult(
  float $pnt[],
  matrix $m[][]
)
{
  float $new[3];
  for ($i = 0; $i < 3; $i++){
    $new[$i] = $pnt[0] * $m[0][$i] +
               $pnt[1] * $m[1][$i] +
               $pnt[2] * $m[2][$i] +
                         $m[3][$i];
  }
  return $new;
}



/*********************************************************************************************************************************
Function:

         vctrMatrix4x4Mult


Parameters:

         vector $pnt - Vector to pass through the matrix
         matrix $m[][] - Matrix/Coordinate space to pass the vector through


Returns:


See Also 

         <fltArryMatrix4x4Mult>
*********************************************************************************************************************************/
global proc vector vctrMatrix4x4Mult(
  vector $pnt,
  matrix $m[][]
)
{
  float $new[3];
  for ($i = 0; $i < 3; $i++){
    $new[$i] = ($pnt.x) * $m[0][$i] +
               ($pnt.y) * $m[1][$i] +
               ($pnt.z) * $m[2][$i] +
                         $m[3][$i];
  }
  vector $ret = <<$new[0], $new[1], $new[2]>>;
  return $ret;
}



/*********************************************************************************************************************************
Function:

         quatToMatrix4x4


Parameters:

         float $q[] - The quaternion to deduce the matrix from


Returns:

         matrix $m[4][4]


See Also <printline>
*********************************************************************************************************************************/
global proc matrix quatToMatrix4x4(
  float $q[]
)
{
  matrix $m[4][4];
  float $x2, $y2, $z2;
  float $xx, $xy, $xz;
  float $yy, $yz, $zz;
  float $wx, $wy, $wz;
  
  $x2 = $q[1] + $q[1];
  $y2 = $q[2] + $q[2];
  $z2 = $q[3] + $q[3];
  
  $xx = $q[1] * $x2;
  $xy = $q[1] * $y2;
  $xz = $q[1] * $z2;
  
  $yy = $q[2] * $y2;
  $yz = $q[2] * $z2;
  $zz = $q[3] * $z2;
  
  $wx = $q[0] * $x2;
  $wy = $q[0] * $y2;
  $wz = $q[0] * $z2;
  
  $m[0][0] = 1.0 - ($yy + $zz);
  $m[1][0] = $xy - $wz;
  $m[2][0] = $xz + $wy;
  $m[3][0] = 0.0;
  
  $m[0][1] = $xy + $wz;
  $m[1][1] = 1.0 - ($xx + $zz);
  $m[2][1] = $yz - $wx;
  $m[3][1] = 0.0;
  
  $m[0][2] = $xz - $wy;
  $m[1][2] = $yz + $wx;
  $m[2][2] = 1.0 - ($xx + $yy);
  $m[3][2] = 0.0;
  
  $m[0][3] = $m[1][3] = $m[2][3] = 0.0;
  $m[3][3] = 1.0;
  
  return $m;
}



/*********************************************************************************************************************************
Function:

         matrix4x4ToFloat16


Parameters:

         matrix $m[][] - The input matrix to convert to a float array


Returns:

         float $return[16] - Float array (size 16) of the matrix


See Also 

         <float16ToMatrix4x4>
*********************************************************************************************************************************/
global proc float[]  matrix4x4ToFloat16(
  matrix $m[][]
)
{
  float $ret[16];
  int $i = 0;
  for ($row = 0; $row < 4; $row++){
    for ($col = 0; $col < 4; $col++){
      $ret[$i] = $m[$row][$col];
      $i++;
    }
  }
  return $ret;
}



/*********************************************************************************************************************************
Function:

         float16ToMatrix4x4


Parameters:

         float $m[16] - The input float array to convert to a matrix


Returns:

         matrix $ret[4][4] - Matrix of the input float array


See Also 

         <matrix4x4ToFloat16>
*********************************************************************************************************************************/
global proc matrix float16ToMatrix4x4(
  float $m[]
)
{
  matrix $ret[4][4];
  int $i = 0;
  for ($row = 0; $row < 4; $row++){
    for ($col = 0; $col < 4; $col++){
      $ret[$row][$col] = $m[$i];
      $i++;
    }
  }
  return $ret;
}



/*********************************************************************************************************************************
Function:

         matrixSetValues4x4


Parameters:

         float $val


Returns:

         matrix $m[4][4]


See Also <printline>
*********************************************************************************************************************************/

global proc matrix matrixSetValues4x4(
  float $val
)
{
  matrix $m[4][4];
  for ($row = 0; $row < 4; $row++){
    for ($col = 0; $col < 4; $col++){
      $m[$row][$col] = $val;
    }
  }
  return $m;
}



/*********************************************************************************************************************************
Function:

         matrixPrint4x4


Parameters:

         matrix $m[][]


Returns:


See Also <printline>
*********************************************************************************************************************************/

global proc matrixPrint4x4(
  matrix $m[][]
)
{
  for ($row = 0; $row < 4; $row++){
    for ($col = 0; $col < 4; $col++){
      print($m[$row][$col]+" ");
    }
    print("\n");
  }
  print("\n");
}



/*********************************************************************************************************************************
Function:

         matrixIdentity4x4


Parameters:


Returns:

         matrix $dientity[4][4]


See Also <printline>
*********************************************************************************************************************************/

global proc matrix matrixIdentity4x4(
)
{
  matrix $identity[4][4];
  $ident = matrixSetValues4x4(0.0);
  $ident[0][0] = 1;
  $ident[1][1] = 1;
  $ident[2][2] = 1;
  $ident[3][3] = 1;
  return $identity;
}			



/*********************************************************************************************************************************
Function:

         lubksb


Parameters:

         matrix $a[][]


Returns:


See Also 

         <ludcmp>
*********************************************************************************************************************************/
global int $MI_indx[];
global float $MI_d;
global float $MI_b[];

global proc lubksb(
  matrix $a
)
{
  global int $MI_indx[];
  global float $MI_b[];

  float $sum;
  int $ii;
  int $ip;
  int $n = 4;
  
  $ii = 0;
  for ($i = 0; $i < $n; $i++){
    $ip = $MI_indx[$i];
    $sum = $MI_b[$ip];
    $MI_b[$ip] = $MI_b[$i];
    if ($ii>=0){
      for ($j = $ii; $j <= $i-1; $j++){
        $sum -= $a[$i][$j] * $MI_b[$j];
      
      }
    } else if ($sum){
      $ii = $i;
    }
    $MI_b[$i] = $sum;
  }
  for ($i = ($n-1); $i >= 0; $i--){
    $sum = $MI_b[$i];
    for ($j = $i+1; $j < $n; $j++){
      $sum -= $a[$i][$j] * $MI_b[$j];
    }
    $MI_b[$i] = $sum / $a[$i][$i];
  }
}



/*********************************************************************************************************************************
Function:

         ludcmp


Parameters:

         matrix $a[][]


Returns:

         matrix $a[][]


See Also 

         <lubksb>
*********************************************************************************************************************************/
global proc matrix ludcmp(
  matrix $a
)
{
  global int $MI_indx[];
  global float $MI_d;

  float $big;
  float $tmp;
  float $sum;
  float $dum;
  int $imax;
  float $vv[];
  int $n = 4;

  $MI_d = 1.0;
  for ($i = 0; $i < $n; $i++){
    $big = 0.0;
    for ($j = 0; $j < $n; $j++){
      if (($tmp = abs($a[$i][$j])) > $big){
        $big = $tmp;
      }
    }
    if ($big == 0.0){
      print("Singular matrix in routine LUDCMP\n");
    }
    $vv[$i] = 1.0 / $big;
  }  

  for ($j = 0; $j < $n; $j++){
    for ($i = 0; $i < $j; $i++){
      $sum = $a[$i][$j];
      for ($k = 0; $k < $i; $k++){
        $sum -= $a[$i][$k] * $a[$k][$j];
      }
      $a[$i][$j] = $sum;
    }
    $big = 0.0;
    for ($i = $j; $i < $n; $i++){
      $sum = $a[$i][$j];
      for ($k = 0; $k < $j; $k++){
        $sum -= $a[$i][$k] * $a[$k][$j];
      }
      $a[$i][$j] = $sum;
      if (($dum = $vv[$i] * abs($sum)) >= $big){
        $big = $dum;
        $imax = $i;
      }
    }
    if ($j != $imax){
      for ($k = 0; $k < $n; $k++){
        $dum = $a[$imax][$k];
        $a[$imax][$k] = $a[$j][$k];
        $a[$j][$k] = $dum;
      }
      $MI_d = -$MI_d;
      $vv[$imax] = $vv[$j];
    }
    $MI_indx[$j] = $imax;
    if ($a[$j][$j] == 0.0){
      $a[$j][$j] = 1.0e-20;
    }
    if ($j != ($n-1)){
      $dum = 1.0 / ($a[$j][$j]);
      for ($i = $j+1; $i < $n; $i++){
        $a[$i][$j] = $a[$i][$j] * $dum;
      }
    }
  }
  return ($a);
}



/*********************************************************************************************************************************
Function:

         matrixInverse4x4


Parameters:

         matrix $a[][] - Input Matrix to invert


Returns:

         matrix $out[4][4]


See Also <printline>
*********************************************************************************************************************************/

global proc matrix matrixInverse4x4(
  matrix $a
)
{
  global int $MI_indx[];
  global float $MI_b[];

  matrix $out[4][4];
 
  // DECOMPOSE THE MATRIX JUST ONCE
  ludcmp($a);

  // FIND THE INVERSE BY COLUMNS
  for ($j = 0; $j < 4; $j++){
    for ($i = 0; $i < 4; $i++){
      $MI_b[$i] = 0.0;
    }
    $MI_b[$j] = 1.0;
    lubksb($a);
    for ($i = 0; $i < 4; $i++){
      $out[$i][$j] = $MI_b[$i];
    }
  }
  
  return ($out);
}



/*********************************************************************************************************************************
Function:

         BO, B1, B2, B3


Parameters:

         float $u - U Value on a curve


Returns:

         Basis position

See Also <printline>
*********************************************************************************************************************************/
// BEZIER BASIS FUNCTIONS (MATCHES A NURB SPAN WITH MULTIPLICITY)
// ----------------------------------------------------------------------------
global proc float B0( float $u)
{
  return ((1.0-$u)*(1.0-$u)*(1.0-$u));
}
global proc float B1( float $u)
{
  return (3.0*$u*((1.0-$u)*(1.0-$u)));
}
global proc float B2( float $u)
{
  return (3.0*$u*$u*(1.0-$u));
}
global proc float B3( float $u)
{
  return ($u*$u*$u);
}



/*********************************************************************************************************************************
Function:

         getBezierInvBasisMatrix4x4


Parameters:

         float $splineUvals[]


Returns:

         matrix $mInverse[4][4]


See Also <printline>
*********************************************************************************************************************************/
global proc matrix getBezierInvBasisMatrix4x4(
  float $splineUvals[]
)
{
  float $u[];
  $u = bubbleSort($splineUvals);
  
  print "USING THE RIGHT ONE.\n";
    
  matrix $m[4][4] = << B0($u[0]), B0($u[1]), B0($u[2]), B0($u[3]);
                       B1($u[0]), B1($u[1]), B1($u[2]), B1($u[3]);
                       B2($u[0]), B2($u[1]), B2($u[2]), B2($u[3]);
                       B3($u[0]), B3($u[1]), B3($u[2]), B3($u[3]) >>;

  matrix $mInverse[4][4] = matrixInverse4x4($m);

  return ($mInverse);
}



/*********************************************************************************************************************************
Function:

         matrixTranspose4x4


Parameters:

         matrix $m[][] - Input Matrix to transpose


Returns:

         matrix $tm[4][4] - Transposed Matrix


See Also <printline>
*********************************************************************************************************************************/
global proc matrix matrixTranspose4x4(
  matrix $m  
)
{
  matrix $tm[4][4];

  $tm[0][0] = $m[0][0];
  $tm[0][1] = $m[1][0];
  $tm[0][2] = $m[2][0];
  $tm[0][3] = $m[3][0];

  $tm[1][0] = $m[0][1];
  $tm[1][1] = $m[1][1];
  $tm[1][2] = $m[2][1];
  $tm[1][3] = $m[3][1];

  $tm[2][0] = $m[0][2];
  $tm[2][1] = $m[1][2];
  $tm[2][2] = $m[2][2];
  $tm[2][3] = $m[3][2];

  $tm[3][0] = $m[0][3];
  $tm[3][1] = $m[1][3];
  $tm[3][2] = $m[2][3];
  $tm[3][3] = $m[3][3];

  return $tm;
}



/*********************************************************************************************************************************
Function:

         matrixRotate4x4


Parameters:

         float $degrees
         string $axis


Returns:

         matrix $m[4][4]


See Also <printline>
*********************************************************************************************************************************/
global proc matrix matrixRotate4x4(
  float $degrees,
  string $axis
)
{
  global float  $gPi;
  float $cosx, $sinx;
  float $angle;

  // CONVERT DEGREES TO RADIANS
  $angle = $degrees * $gPi / 180.0;
  matrix $m[4][4] = matrixIdentity4x4();

  $cosx = cos($angle);
  $sinx = sin($angle);

  switch ($axis) {
    case "x":
    case "X":
      $m[0][0] = $m[3][3] = 1.;
      $m[1][1] = $m[2][2] = $cosx;
      $m[1][2] = $sinx;
      $m[2][1] = -$sinx;
      break;
    case "y":
    case "Y":
      $m[1][1] = $m[3][3] = 1.;
      $m[0][0] = $m[2][2] = $cosx;
      $m[0][2] = -$sinx;
      $m[2][0] = $sinx;
      break;
    case "z":
    case "Z":
      $m[2][2] = $m[3][3] = 1.;
      $m[0][0] = $m[1][1] = $cosx;
      $m[0][1] = $sinx;
      $m[1][0] = -$sinx;
      break;
  }
  return $m;
}



/*********************************************************************************************************************************
Function:

         determinant


Parameters:

         matrix $mMatrix
         int $size


Returns:

         float $determinant


See Also <printline>
*********************************************************************************************************************************/
// ANOTHER MATRIX INVERSION METHOD
// ---------------------------------------------------------------------------
// DERIVES THE DETERMINANT OF THE PROVIDED $mMatrix.
// THE $size MUST BE SPECIFIED (2, 3 or 4).
proc float determinant(
  matrix $mMatrix,
  int $size
)
{
  float $determinant = 0.0;

  if ( $size == 2 ) {
    $determinant = ( ( $mMatrix[0][0] * $mMatrix[1][1] ) -
                     ( $mMatrix[0][1] * $mMatrix[1][0] ) );
  }
  if ( $size == 3 ) {
    $determinant = ( $mMatrix[0][0] * $mMatrix[1][1] * $mMatrix[2][2] +
                     $mMatrix[1][0] * $mMatrix[2][1] * $mMatrix[0][2] +
                     $mMatrix[0][1] * $mMatrix[1][2] * $mMatrix[2][0] ) -
                   ( $mMatrix[2][0] * $mMatrix[1][1] * $mMatrix[0][2] +
                     $mMatrix[1][0] * $mMatrix[0][1] * $mMatrix[2][2] +
                     $mMatrix[2][1] * $mMatrix[1][2] * $mMatrix[0][0] );

  }
  if ( $size == 4 ) {
    // Determinant of 4×4 matrix:
    //    < A B C D >                < 00 01 02 03 >
    //    < E F G H >                < 10 11 12 13 >
    //    < I J K L >                < 20 21 22 23 >
    //    < M N O P >                < 30 31 32 33 >
    //
    // Use first row < A B C D > and use Laplace Expansion
    // against corresponding 3×3 matrices
    //
    //  det = ( A | < F G H > | ) -
    //        ( B | < E G H > | ) +
    //        ( C | < E F H > | ) -
    //        ( D | < E F G > | )
    //
    // < J K L >  < I K L >  < I J L >  < I J K >
    // < N O P >  < M O P >  < M N P >  < M N O >
    // 
    //  det = ( A | < 11 12 13 > | ) -
    //        ( B | < 10 12 13 > | ) +
    //        ( C | < 10 11 13 > | ) -
    //        ( D | < 10 11 12 > | )
    //
    // < 21 22 23 > < 20 22 23 > < 20 21 23 > < 20 21 22 >
    // < 31 32 33 > < 30 32 33 > < 30 31 33 > < 30 31 32 >

    float $detA, $detB, $detC, $detD;

    $detA =  ( $mMatrix[1][1] * $mMatrix[2][2] * $mMatrix[3][3] +
               $mMatrix[1][2] * $mMatrix[2][3] * $mMatrix[3][1] +
               $mMatrix[1][3] * $mMatrix[2][1] * $mMatrix[3][2] ) -
             ( $mMatrix[1][1] * $mMatrix[2][3] * $mMatrix[3][2] +
               $mMatrix[1][2] * $mMatrix[2][1] * $mMatrix[3][3] +
               $mMatrix[1][3] * $mMatrix[2][2] * $mMatrix[3][1] );
  
    $detB =  ( $mMatrix[1][0] * $mMatrix[2][2] * $mMatrix[3][3] +
               $mMatrix[1][2] * $mMatrix[2][3] * $mMatrix[3][0] +
               $mMatrix[1][3] * $mMatrix[2][0] * $mMatrix[3][2] ) -
             ( $mMatrix[1][0] * $mMatrix[2][3] * $mMatrix[3][2] +
               $mMatrix[1][2] * $mMatrix[2][0] * $mMatrix[3][3] +
               $mMatrix[1][3] * $mMatrix[2][2] * $mMatrix[3][0] );
  
    $detC =  ( $mMatrix[1][0] * $mMatrix[2][1] * $mMatrix[3][3] +
               $mMatrix[1][1] * $mMatrix[2][3] * $mMatrix[3][0] +
               $mMatrix[1][3] * $mMatrix[2][0] * $mMatrix[3][1] ) -
             ( $mMatrix[1][0] * $mMatrix[2][3] * $mMatrix[3][1] +
               $mMatrix[1][1] * $mMatrix[2][0] * $mMatrix[3][3] +
               $mMatrix[1][3] * $mMatrix[2][1] * $mMatrix[3][0] );
  
    $detD =  ( $mMatrix[1][0] * $mMatrix[2][1] * $mMatrix[3][2] +
               $mMatrix[1][1] * $mMatrix[2][2] * $mMatrix[3][0] +
               $mMatrix[1][2] * $mMatrix[2][0] * $mMatrix[3][1] ) -
             ( $mMatrix[1][0] * $mMatrix[2][2] * $mMatrix[3][1] +
               $mMatrix[1][1] * $mMatrix[2][0] * $mMatrix[3][2] +
               $mMatrix[1][2] * $mMatrix[2][1] * $mMatrix[3][0] );
  
    $determinant = ( $mMatrix[0][0] * $detA ) -
                   ( $mMatrix[0][1] * $detB ) +
                   ( $mMatrix[0][2] * $detC ) -
                   ( $mMatrix[0][3] * $detD );
  }

  return $determinant;
}



/*********************************************************************************************************************************
Function:

         transpose


Parameters:

         matrix $aMatrix
         int $size


Returns:

         matrix $tMatrix[4][4]


See Also <printline>
*********************************************************************************************************************************/
// RETURNS A MATRIX WHICH IS THE TRANSPOSE OF $aMatrix.
// THE $size MUST BE SPECIFIED (TYPICALLY 3 OR 4 FOR THIS SCRIPT).
proc matrix transpose(
  matrix $aMatrix,
  int $size
)
{
  matrix $tMatrix[4][4] = << 0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0 >>;

  for ( $i = 0; $i < $size; $i++ ) {
    for ( $j = 0; $j < $size; $j++ ) {
      $tMatrix[$i][$j] = $aMatrix[$j][$i];
    }
  }
  return $tMatrix;
}



/*********************************************************************************************************************************
Function:

         mCRC


Parameters:

         matrix $aMatrix
         int $size
         int $row
         int $column

Returns:

         matrix $cMatrix[4][4]


See Also <printline>
*********************************************************************************************************************************/

// REMOVES THE SPECIFIED $row AND $column FROM $aMatrix;
// e.g. COLLAPSES A 4×4 MATRIX TO A 3×3 MATRIX.
// NOTE: $row & $column ARE 1-INDEX
proc matrix mCRC(
  matrix $aMatrix,
  int $size,
  int $row,
  int $column
) 
{
  matrix $cMatrix[4][4] = << 0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0 >>;

  for ( $i = 0, $m = 0; $i < $size; $i++, $m++ ) {
    // SKIP OVER COLLAPSED ROW
    if ( $m+1 == $row ) {
      $m++;                                                    
    }
    for ( $j = 0, $n = 0; $j < $size; $j++, $n++ ) {
      // Skip over collapsed column
      if ( $n+1 == $column ) {
        $n++;
      }
      if ( $m < 4 && $n < 4 ) {
        $cMatrix[$i][$j] = $aMatrix[$m][$n];
      } else {
        $cMatrix[$i][$j] = 0.0;
      }
    }
  }
  return $cMatrix;
}



/*********************************************************************************************************************************
Function:

         adjoint


Parameters:

         matrix $aMatrix
         int $size


Returns:

         matrix $cMatrix[4][4]


See Also <printline>
*********************************************************************************************************************************/
// RETURNS A MATRIX WHICH IS THE ADJOINT OF $aMatrix.
// THE $size OF THE INPUT MATRIX MUST BE SPECIFIED (3 or 4).
proc matrix adjoint(
  matrix $aMatrix,
  int $size
)
{
  matrix $cMatrix[4][4] = << 0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0 >>;

  int $detSize = ( $size - 1 );
  if ( $size > 2 ) {
    // Cofactor of top-left 3×3 matrix
    $cMatrix[0][0] =  determinant( mCRC( $aMatrix, $size, 1, 1 ), $detSize );
    $cMatrix[0][1] = -determinant( mCRC( $aMatrix, $size, 1, 2 ), $detSize );
    $cMatrix[0][2] =  determinant( mCRC( $aMatrix, $size, 1, 3 ), $detSize );

    $cMatrix[1][0] = -determinant( mCRC( $aMatrix, $size, 2, 1 ), $detSize );
    $cMatrix[1][1] =  determinant( mCRC( $aMatrix, $size, 2, 2 ), $detSize );
    $cMatrix[1][2] = -determinant( mCRC( $aMatrix, $size, 2, 3 ), $detSize );

    $cMatrix[2][0] =  determinant( mCRC( $aMatrix, $size, 3, 1 ), $detSize );
    $cMatrix[2][1] = -determinant( mCRC( $aMatrix, $size, 3, 2 ), $detSize );
    $cMatrix[2][2] =  determinant( mCRC( $aMatrix, $size, 3, 3 ), $detSize );
  }

  if ( $size > 3 ) {
    // COFACTOR OF 4TH COLUMN
    $cMatrix[0][3] = -determinant( mCRC( $aMatrix, $size, 1, 4 ), $detSize );
    $cMatrix[1][3] =  determinant( mCRC( $aMatrix, $size, 2, 4 ), $detSize );
    $cMatrix[2][3] = -determinant( mCRC( $aMatrix, $size, 3, 4 ), $detSize );

    // COFACTOR OF 4TH ROw
    $cMatrix[3][0] = -determinant( mCRC( $aMatrix, $size, 4, 1 ), $detSize );
    $cMatrix[3][1] =  determinant( mCRC( $aMatrix, $size, 4, 2 ), $detSize );
    $cMatrix[3][2] = -determinant( mCRC( $aMatrix, $size, 4, 3 ), $detSize );
    $cMatrix[3][3] =  determinant( mCRC( $aMatrix, $size, 4, 4 ), $detSize );
  }

  // ADJOINT IS TRANSPOSE OF MATRIX CONTAINING COFACTORS
  $cMatrix = transpose( $cMatrix, $size );

  return $cMatrix;
}



/*********************************************************************************************************************************
Author: James Tooley
Date:   May 16, 2008
*********************************************************************************************************************************/
global proc float ROWCOL(
  matrix $a[][],
  matrix $b[][],
  int $row,
  int $col
)
{
  float $retVal = $a[$row][0] * $b[0][$col] +
                  $a[$row][1] * $b[1][$col] +
                  $a[$row][2] * $b[2][$col] +
                  $a[$row][3] * $b[3][$col];
  return $retVal;
}
  
global proc matrix matrixMult4x4(
  matrix $a[][],
  matrix $b[][]
)
{
  float $v0  = ROWCOL($a, $b, 0, 0);
  float $v1  = ROWCOL($a, $b, 0, 1);
  float $v2  = ROWCOL($a, $b, 0, 2);
  float $v3  = ROWCOL($a, $b, 0, 3);
  float $v4  = ROWCOL($a, $b, 1, 0);
  float $v5  = ROWCOL($a, $b, 1, 1);
  float $v6  = ROWCOL($a, $b, 1, 2);
  float $v7  = ROWCOL($a, $b, 1, 3);
  float $v8  = ROWCOL($a, $b, 2, 0);
  float $v9  = ROWCOL($a, $b, 2, 1);
  float $v10 = ROWCOL($a, $b, 2, 2);
  float $v11 = ROWCOL($a, $b, 2, 3);
  float $v12 = ROWCOL($a, $b, 3, 0);
  float $v13 = ROWCOL($a, $b, 3, 1);
  float $v14 = ROWCOL($a, $b, 3, 2);
  float $v15 = ROWCOL($a, $b, 3, 3);

  matrix $retMtx[4][4] = << $v0,  $v1,  $v2,  $v3;
                            $v4,  $v5,  $v6,  $v7;
                            $v8,  $v9,  $v10, $v11;
                            $v12, $v13, $v14, $v15 >>;
  return $retMtx;
}
/*********************************************************************************************************************************
Author:
         Tim Naylor

Function:

         multiplyMatrix


Parameters:

         matrix $aMatrix
         matrix $bMatrix


Returns:

         matrix $cMatrix[4][4]


See Also <printline>
*********************************************************************************************************************************/
// RETURNS A MATRIX WHICH IS THE PRODUCT OF MULTIPLYING $aMatrix AND $bMatrix.
// NOTE: AS MATRICES IN THIS SCRIPT ARE ALWAYS SQUARE (3×3 OR 4×4)
// NO ERROR-CHECKING IS SUPPLIED TO ASSERT THAT THE SIZES OF THE TWO
// MATRICES ARE APPLICABLE FOR MULTIPLICATION.

global proc matrix multiplyMatrix(
  matrix $aMatrix,
  matrix $bMatrix
)
{
  matrix $cMatrix[4][4] = << 0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0 >>;

  for ( $i = 0; $i < 4; $i++ ) {
    for ( $j = 0; $j < 4; $j++ ) {
      for ( $k = 0; $k < 4; $k++ ) {
        $cMatrix[$i][$j] = $cMatrix[$i][$j] +
                         ( $aMatrix[$i][$k] *
                           $bMatrix[$k][$j] );
      }
    }
  }
  return $cMatrix;
}



/*********************************************************************************************************************************
Function:

         inverse


Parameters:

         matrix $aMatrix
         int $size


Returns:

         matrix $iMatrix[4][4]


See Also 

         <matrixInverse>
*********************************************************************************************************************************/
// RETURNS A MATRIX WHICH IS THE INVERSE OF $aMatrix.
// THE $SIZE OF THE INPUT MATRIX MUST BE SPECIFIED (3 or 4).
proc matrix inverse( 
matrix $aMatrix, 
int $size )
{
  matrix $iMatrix[4][4] = << 0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0;
                             0, 0, 0, 0 >>;

  float $determinant = determinant( $aMatrix, $size );

  if ( $determinant != 0.0 )
    $iMatrix = ( 1 / determinant( $aMatrix, $size ) ) *
               adjoint( $aMatrix, $size );

  return $iMatrix;
}



/*********************************************************************************************************************************
Function:

         matrixInverse


Parameters:

         matrix $aMatrix


Returns:

         matrix $iMatrix[4][4]


See Also 

         <inverse>
*********************************************************************************************************************************/
global proc matrix matrixInverse(
  matrix $aMatrix
)
{
  matrix $iMatrix[4][4];
  float $determinant;

  $iMatrix = inverse( $aMatrix, 4);

  if ( 0 == determinant( $aMatrix, 4) ) {
    $message = "Matrix A is singular and has no inverse";
    $iMatrix = matrixIdentity4x4();
  }

  return $iMatrix;
}
