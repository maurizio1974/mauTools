//------------------------------------------------------------------------------------------------------------//
//------------------------------------------------------------------------------------------------------------//
//	SCRIPT:	refEditReplaceUI.mel
//	VERSION: 1.0
//	AUTHOR:	Maurizio Giglioli
//		    maurizio1974@gmail.com
//	DATE:         10 02 2010
//  Last Updated: 
//
//
//	DESCRIPTION:	UI FOR REFERENCE REPLACE TOOL
//
//
//	INSTRUCTIONS:	source the Script
//			        call the procedure refEditReplaceUI()
//
//------------------------------------------------------------------------------------------------------------//
//------------------------------------------------------------------------------------------------------------//

python ("import maya.cmds as mc");
python ("import maya.mel as mel");
python ("import os");
python ("import sys");
python ("import string");

python("path = '/u/mng/maya/scripts/utility/'"); // Change with the path where you put the script
python ("if path is not (sys.path):sys.path.append(path)");

python "import refEditReplace";
python("reload(refEditReplace)");

global proc refEditReplaceUI()
{
    string $proj[] = `workspace -q -lfw`;
    
    if (`window -ex refEditReplaceWIN`)
        deleteUI refEditReplaceWIN;
        
    window -t "Reference Replacer" -wh 320 210 refEditReplaceWIN;
    formLayout refEditReplaceFL;
    
        textField -tx ($proj[0]+"/scenes/") locTXF;
        button -l "Scene File Directory" -c "refFileBrowser 1" locBT;
        textField newLocTXF;
        button -l "Reference File Directory" -c "refFileBrowser 2" newLocBT;
        textField -tx "copy" newDirTXF;
        text -l "Copy Folder" newDirTX;
        button -l "GO" -c "refReplaceCMD" goBT;
        
    formLayout -e
    
        -af locTXF "left" 5 -af locTXF "top" 20 -af locTXF "right" 5
        -af locBT "left" 5 -ac locBT "top" 5 locTXF -af locBT "right" 5
        -af newLocTXF "left" 5 -ac newLocTXF "top" 5 locBT -af newLocTXF "right" 5
        -af newLocBT "left" 5 -ac newLocBT "top" 5 newLocTXF -af newLocBT "right" 5
        -af newDirTX "left" 5 -ac newDirTX "top" 5 newLocBT -ap newDirTX "right" 5 25
        -ac newDirTXF "left" 5 newDirTX -ac newDirTXF "top" 5 newLocBT -af newDirTXF "right" 5
        -af goBT "left" 5 -ac goBT "top" 5 newDirTXF -af goBT "right" 5
        
    refEditReplaceFL;
    
    showWindow refEditReplaceWIN;
    window -e -wh 320 210 refEditReplaceWIN;
}


// FILE BROWSER PROCEDURE
global proc refFileBrowser(int $which)
{
    if ($which == 1) // Switch for the file browser to select a maya scene
        fileBrowserDialog -m 0 -fc "mayaSceneLoc" -ft "mayaAscii" -an "Maya Scene";
    if ($which == 2) // Switch for the file browser to select a directory for the new reference files 
        fileBrowserDialog -m 4 -fc "mayaRefLoc" -ft "directory" -an "Select directory";
}

// GLOBAL PROC TO GET THE FILE BROWSER PATH FOR THE MAYA SCENE
global proc mayaSceneLoc(string $result, string $type)
{
    textField -e -tx $result "locTXF";
}

// GLOBAL PROC TO GET THE FILE BROWSER PATH FOR THE REFERENCE FILE
global proc mayaRefLoc(string $result, string $type)
{
    textField -e -tx $result "newLocTXF";
}

// PROCEDURE TO REPLACE THE REFERENCE FILE IN THE SELECTED MAYA SCENE
// WITH A PYTHON SCRIPT
global proc refReplaceCMD()
{
    string $buf[], $loc , $slash;
    string $mayaScene = `textField -q -tx "locTXF"`;
    string $newLoc = `textField -q -tx "newLocTXF"`;
    string $newDir = `textField -q -tx "newDirTXF"`;
    string $system = `python "sys.platform"`;
    
    // DEPENDING ON THE SYSTEM THE DIRECTORY SLASHES ARE OPPOSED
    if($system == "win32")
        $slash = "/";
    if($system == "linux2")
        $slash = "/";
    
    if ($mayaScene != "") // Check if a maya scene has been selectd
    {
        if(!`gmatch $mayaScene "*.ma"`)
        {
            confirmDialog -t "Warning" -m "Make sure to select a maya ASCII scene" -b "OK" -db "OK" ;
        }else{
            if ($newLoc != "") // Check if a directory for the new reference files has been selected
            {
                if (size($newDir) != 0 )
                {
                    tokenize($mayaScene,$slash,$buf);
                    if($system == "linux2")
                        $loc = "/"+$buf[0]+$slash;
                    if($system == "win32")
                        $loc = $buf[0]+$slash;
                    for($i=1;$i<(size($buf)-1);$i++) // rebuilt the maya scene path without the maya scnee name 
                        $loc += $buf[$i]+$slash;
                    
                    // CREATE PYTHON VARIABLES TO FEED INTO THE PYTHON PROCEDURE    
                    python ("loc = '"+$loc+"'");
                    python ("newLoc = '"+($newLoc+$slash)+"'");
                    python ("file = '"+($buf[size($buf)-1])+"'");
                    python ("newDir = '"+($newDir+$slash)+"'");
                    
                    print ("refEditReplace.repl( "+$loc+", "+$newLoc+", "+$buf[size($buf)-1]+", "+$newDir+")\n"); // Print the Python Command
                    python ("refEditReplace.repl(loc, newLoc, file, newDir)"); // Python Command
                }else{
                    confirmDialog -t "Warning" -m "Make sure to set a new directory where to save the modified file." -b "OK" -db "OK" ;
                }
            }else{
                confirmDialog -t "Warning" -m "Make sure to select the location of the reference files" -b "OK" -db "OK" ;
            }
        }
    }else{
        confirmDialog -t "Warning" -m "Make sure to select a maya scene" -b "OK" -db "OK" ;
    }
}
