global proc string Quadsphere_CreateMenu(string $after) {
    string $menu;
    if ($after == "") {
        $menu = `menuItem -p "polyPrimitivesItem" -label "Quad Sphere" -image "quadsphere.png" "quadsphereCreateMenuItem"`;
    } else {
        $menu = `menuItem -p "polyPrimitivesItem" -ia $after -label "Quad Sphere" -image "quadsphere.png" "quadsphereCreateMenuItem"`;
    }

    menuItem -e -command "Quadsphere_CreateFromOptions()" $menu;
    menuItem -p "polyPrimitivesItem" -ia $menu -ob true -c "Quadsphere_CreateOptions()";

    return $menu;
}

global proc Quadsphere_CreateQuadsphere(
    int $division,
    float $radius,
    float $spherical
) {
    polyQuadsphere -division $division -radius $radius -spherical $spherical;
}

global proc Quadsphere_UIUninitiate() {
    global string $gQuadsphereMenu;
    if (`menuItem -exists $gQuadsphereMenu`) {
        deleteUI $gQuadsphereMenu;
    }
}

global proc Quadsphere_UIInit() {
    global string $gQuadsphereMenu;
    if (`menuItem -exists $gQuadsphereMenu`) {
        deleteUI $gQuadsphereMenu;
    }

    if (`menu -exists "polyPrimitivesItem"`) {
        $gQuadsphereMenu = "";

        string $menuItems[] = `menu -q -ia "polyPrimitivesItem"`;
        string $menuItem;
        int $i;
        int $divIdx = 0;
        for ($i = 0; $i < size($menuItems) && $gQuadsphereMenu == ""; $i++) {
            $menuItem = $menuItems[$i];
            if (`menuItem -q -divider $menuItem`) {
                $divIdx++;
                if ($divIdx == 2) {
                    $gQuadsphereMenu = Quadsphere_CreateMenu($menuItems[$i - 1]);
                }
            } else if (`menuItem -q -isCheckBox $menuItem`) {
                $gQuadsphereMenu = Quadsphere_CreateMenu($menuItems[$i - 1]);
            }
        }

        if ($gQuadsphereMenu == "") {
            $gQuadsphereMenu = Quadsphere_CreateMenu("");
        }
    }
}

/* -------------------- create options -------------------- */
global proc Quadsphere_CreateOptions() {
    // name of command for option box
    string $command = "QuadsphereOptions";

    string $callback = $command + "_Callback";
    string $option = $command + "_InitOptionVars";
    string $setup = $command + "_CreateControlSetup";

    // get option box
    string $optBoxRootLayout = getOptionBox();
    setParent $optBoxRootLayout;

    // set option box command name
    setOptionBoxCommandName($command);

    //  activate the default UI template.
    setUITemplate -pushTemplate DefaultTemplate;

    //  turn on the wait cursor.
    waitCursor -state 1;

    // create option box content
    tabLayout -tabsVisible 0 -scrollable 1 -innerMarginWidth 4;

    string $parentLayout = `columnLayout -adjustableColumn 1 -rowSpacing 4`;

    intSliderGrp -label "Division" -field true -v 10 -s 1 -min 2 quadsphereOptionDivisionSlider;
    floatSliderGrp -label "Radius" -field true -v 1.0 -min 0.1 -max 10.0 -fmx 99999.0 quadsphereOptionRadiusSlider;
    floatSliderGrp -label "Spherical" -field true -v 1.0 -min 0.0 -max 1.0 quadsphereOptionSphericalSlider;

    setParent ..;

    //  turn off the wait cursor.
    waitCursor -state 0;

    // deactive the default UI template
    setUITemplate -popTemplate;

    // customize buttons

    // 'Apply' button
    string $applyBtn = getOptionBoxApplyBtn();
    button -edit
        -command ($callback + "(\"" + $parentLayout + "\")")
        $applyBtn;

    // 'ApplyAndClose' button
    string $applyCloseBtn = getOptionBoxApplyAndCloseBtn();
    button -edit
        -label "Create"
        -command ($callback + "(\"" + $parentLayout + "\"); hideOptionBox")
        $applyCloseBtn;

    // 'Reset' button
    string $resetBtn = getOptionBoxResetBtn();
    button -edit
        -command ($option + "(1); " + $setup + "(\"" + $parentLayout + "\")")
        $resetBtn;

    // set the option box title.
    setOptionBoxTitle("Quadsphere Options");

    // customize the 'Help' menu item text.
    setOptionBoxHelpTag( "QuadsphereOptions" );

    eval(($option + "(0); " + $setup + "(\"" + $parentLayout + "\")"));

    // display option box
    showOptionBox();
}

global proc QuadsphereOptions_InitOptionVars(int $override) {
    if ($override || !`optionVar -exists "qsDivision"`) {
        optionVar -iv "qsDivision" 10;
    }

    if ($override || !`optionVar -exists "qsRadius"`) {
        optionVar -fv "qsRadius" 1.0;
    }

    if ($override || !`optionVar -exists "qsSpherical"`) {
        optionVar -fv "qsSpherical" 1.0;
    }
}

global proc QuadsphereOptions_CreateControlSetup(string $parent) {
    setParent $parent;

    // load option vars
    int $divisionVar = `optionVar -q "qsDivision"`;
    float $radiusVar = `optionVar -q "qsRadius"`;
    float $sphericalVar = `optionVar -q "qsSpherical"`;

    // update control default values
    intSliderGrp -e -v $divisionVar quadsphereOptionDivisionSlider;
    floatSliderGrp -e -v $radiusVar quadsphereOptionRadiusSlider;
    floatSliderGrp -e -v $sphericalVar quadsphereOptionSphericalSlider;
}

global proc QuadsphereOptions_Callback(string $parent) {
    // query control values
    int $division = `intSliderGrp -q -v quadsphereOptionDivisionSlider`;
    float $radius = `floatSliderGrp -q -v quadsphereOptionRadiusSlider`;
    float $spherical = `floatSliderGrp -q -v quadsphereOptionSphericalSlider`;

    // update option vars
    optionVar -iv "qsDivision" $division;
    optionVar -fv "qsRadius" $radius;
    optionVar -fv "qsSpherical" $spherical;

    Quadsphere_CreateQuadsphere($division, $radius, $spherical);
}

global proc Quadsphere_CreateFromOptions() {
    QuadsphereOptions_InitOptionVars(false);

    int $division = `optionVar -q "qsDivision"`;
    float $radius = `optionVar -q "qsRadius"`;
    float $spherical = `optionVar -q "qsSpherical"`;

    Quadsphere_CreateQuadsphere($division, $radius, $spherical);
}